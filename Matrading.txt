//------------------------------------------------------------------------
// 简称: 公式模板
// 名称: template
// 类别: 公式应用
// 类型: 用户应用
// 输出: Void
//------------------------------------------------------------------------
Params
	//此处添加参数
	Integer nEntries(1);
	Numeric incNate(1);
	Numeric failopen(0);
	Numeric fallback(0);

	Integer SyncEntries(1);
Vars
///////////////////////////////////////////////////////////////////////////	
	Numeric FastLength(5);// 短期指数平均线参数
	Numeric SlowLength(20);// 长期指数平均线参数	
	
	Numeric daytimeStart(0915);
	Numeric daytimeEnd(1450);
	Numeric nighttimeStart(2115);
	Numeric nightEnd(2250);
///////////////////////////////////////////////////////////////////////////	
	Numeric RiskRatio(2);
	Bool    SyncWithPastSignal(True);
///////////////////////////////////////////////////////////////////////////
	
	global Integer limitedEntries(1);
	global Bool sendorder;
	global Numeric currentPosition;
	global Integer LastMarketPosition;
	global Numeric MRatio;
	

	global Numeric orderPrice;
	global Numeric TotalEquity;
	global Numeric openUnits;
	global Integer secCount(1);

	Series<Numeric> AvgTR;
	
	Bool closetrading;
	Bool opentrading;
	Bool failclose;
	Bool fallbackclose;
	Bool CloseLong;
	Bool CloseShort;
	Bool OpenLong;
	Bool OpenShort;		

	Series<Numeric> KV;
	Series<Numeric> DV;
	Series<Numeric> LOWV;
	Series<Numeric> HIGHV;
	Series<Numeric> RSV;

	Series<Numeric> H5;
	Series<Numeric> H20;	
	Series<Numeric> L5;
	Series<Numeric> L20;	


Defs 	//此处添加公式函数

	Bool willCloseLong(){
		If(L20 < L20[1] and longCurrentContracts >0)
		{
			return True;
		}	

		if (kv < 70 and longCurrentContracts >0)
		{
			return True;
		}

		return false;
	}

	Bool willCloseShort(){

		If(H20 > H20[1] and shortCurrentContracts >0)
		{
			return true;
		}

		if (kv > 30 and shortCurrentContracts >0)
		{
			return True;
		}


		return false;
	}

	Bool willOpenLong(){
		
		If(close >H20[1] and longCurrentContracts ==0  ) //and High[1] <= H20[2] and (H5 - L5) >= (AvgTR *2) and  H20-L20 < (AvgTR *3)
		{
			return True;
		}

		//if (KV > DV and shortCurrentContracts ==0 and KV > 50 and  kv < 80)
		//{
			//return True;
		//}
//
		return false;
	}

	Bool willOpenShort(){

		If(close < L20[1] and shortCurrentContracts ==0) //  and Low[1] >= L20[2] and (H5 - L5) >= (AvgTR *2)   and  H20-L20 < (AvgTR *3)
		{
			return True;
		}	
		
		//if (KV < DV and shortCurrentContracts ==0 and KV > 20 and  kv < 50)
		//{
			//return True;
		//}
		
		return false;
	}
	
	Numeric updateMarginRatio(){
		
        MarginRate rate;
        //获取账户对应合约的保证金率		
		Bool ret = A_GetMarginRate(Symbol, rate);
        MRatio = Max(rate.longMarginRatio, rate.shortMarginRatio);
        
        Return MRatio;	
	}

	
	Numeric getUnitsNumber(Numeric price, Numeric atr){
		
		if(price ==0 or atr == 0 or MRatio ==0)
			return 0;
		
		
		Numeric totalequity = Portfolio_CurrentEquity();
		
		Numeric riskmargin = atr * ContractUnit()*BigPointValue(); // 每手 atr 波动的净值波动
		Numeric unitmargin = price * ContractUnit() * BigPointValue() * MRatio; // 每手保证金

		if(riskmargin ==0 or unitmargin == 0)
			return 0;
		
		
		Numeric atrunits = (totalequity*RiskRatio/100) /riskmargin;
		atrunits = IntPart(atrunits); 

		Numeric marginunits = (totalequity*RiskRatio/100) / unitmargin;
		marginunits = IntPart(marginunits); 

		Return Min(marginunits, atrunits);
	}
	
//返回值如下定义：
//0 日线
//1 分钟线
//2 TICK线
//3 量线（暂不支持）
//4 周线
//5 月线
//当前数据周期为6日线，Frequency为6d，BarType等于0，BarInterval等于6；
//当前数据周期为70分钟线，Frequency为70m，BarType等于1，BarInterval等于70；
//当前数据周期为1TICK线，Frequency为tick，BarType等于2，BarInterval等于1；
//当前数据周期为10秒线，Frequency为10s，BarType等于2，BarInterval等于20；
//当前数据周期为1周线，Frequency为1w，BarType等于4，BarInterval等于1；
//当前数据周期为1月线，Frequency为1mon，BarType等于5，BarInterval等于1；
//当前数据周期为N秒(N >=60)、N日(N>=7)，建议使用Frequency，表达周期更为准确。

	Bool OpenTradingTime(){
		if (BarType == 1 or BarType == 2 or BarType == 3){

			TimeStamp ttime = EndTime;
			//if (BarStatus ==2){
				//ttime = CurrentTime;
			//} 

			return (ttime > daytimeStart/ 10000 and ttime < daytimeEnd/10000) Or ( ttime > nighttimeStart/10000 and  ttime < nightEnd/10000) ;
		}
			
		return true;
	}

	Bool CloseTradingTime(){
		
		if (BarType == 1 or BarType == 2){
			
			TimeStamp ttime = EndTime;
			//if (BarStatus ==2){
				//ttime = CurrentTime;
			//} 
			
			return (ttime > daytimeEnd/10000 and  ttime < 0.1459) or (ttime > nightEnd/10000 and  ttime < 0.2259) ;
		}
			
		return false;
	}


	Bool closeWithBuyToCover(Numeric lot, Numeric price){
		    Array<Integer> orders;
            //指定账户指定合约开多头单
            
            if (lot >0)
				return A_BuyToCover(Symbol, lot, price, orders, "", "");

            return True;
	}
	
	Bool closeWithSell(Numeric lot, Numeric price){
		    Array<Integer> orders;

            //指定账户指定合约开多头单
            
            if (lot >0)
				return A_Sell(Symbol, lot, price, orders, "", "");

            return True;
	}
	
	Bool pureSellShort(Numeric lot, Numeric price){
		    Array<Integer> orders;
            //指定账户指定合约开多头单
            if (lot >0)
				return A_SellShort(Symbol, lot, price, orders, "", "");

            return true;
	}

	Bool pureBuy(Numeric lot, Numeric price){
		    Array<Integer> orders;
            //指定账户指定合约开多头单
            if (lot >0)
				return A_Buy(Symbol, lot, price, orders, "", "");
		
			return True;
	}
	
	Bool isCloseOnFail(Numeric failcheck){

		if (failcheck <=0 or MarketPosition ==0)
			return false;
		
		if (MarketPosition >0 and LastEntryPrice > Close + failcheck)
			return true;

		if (MarketPosition <0 and LastEntryPrice < Close - failcheck)
			return true;
												
		return False;
	}
		
	Bool isCloseOnSuccess(Numeric successcheck, Numeric N){
		

		if (successcheck <=0 or N <=0 or MarketPosition ==0 )
			return false;		
		
		Numeric lowv= NthLower(Low[1],N,1);
		Numeric highv=NthHigher(High[1], N, 1);
		
		Commentary("isCloseOnSuccess N=" + Text(N) + "  highv=" + Text(highv) + "  lowv=" + Text(lowv)+ "  successcheck=" + Text(successcheck));
		
		if (MarketPosition >0 And highv > LastEntryPrice and  highv > Close + successcheck)
			return true;

		if (MarketPosition <0 and lowv < LastEntryPrice  and lowv < Close - successcheck)
			return true;		
		
		return False;
	}
	
	Bool refreshCalculation(){
			AvgTR = AvgTrueRange(10);
			openUnits = getUnitsNumber(Close, AvgTR);
			Commentary("OnBarClose AvgTR =" + Text(AvgTR));
			
			H5 = Highest(Close,FastLength);
			H20 = Highest(Close,SlowLength);
			
			L5 = Lowest(Close,FastLength);
			L20 = Lowest(Close,SlowLength);		
			
			PlotNumeric("H5",H5);
			PlotNumeric("H20",H20);
			PlotNumeric("L5",L5);
			PlotNumeric("L20",L20);	
					
			Commentary("refreshCalculation H5 =" + Text(H5));
			Commentary("refreshCalculation H20 =" + Text(H20));
			Commentary("refreshCalculation L5 =" + Text(L5));
			Commentary("refreshCalculation L20 =" + Text(L20));			

			LOWV=Lowest(LOW,10);
			HIGHV=Highest(HIGH,10);
			RSV=average((CLOSE-LOWV)/(HIGHV-LOWV)*100,3);
			KV=xaverage(RSV,3);
			DV=average(KV,3);
			Commentary("refreshCalculation KV =" + Text(KV));
			Commentary("refreshCalculation DV =" + Text(DV));			


			closetrading = CloseTradingTime();
			opentrading  = OpenTradingTime();

			failclose = isCloseOnFail(AvgTR * failopen);
			fallbackclose = isCloseOnSuccess(AvgTR[1] * fallback, 10);
			Commentary("refreshCalculation failclose =" + Text(IIF (failclose, 1 , 0)));
			Commentary("refreshCalculation fallbackclose =" + Text(IIF (fallbackclose, 1 , 0)));
			
			CloseLong = willCloseLong();
			CloseShort = willCloseShort();
			OpenLong = willOpenLong();
			OpenShort = willOpenShort();
			
			Commentary("refreshCalculation OpenLong =" + Text(IIF (OpenLong, 1 , 0)));
			Commentary("refreshCalculation OpenShort =" + Text(IIF (OpenShort, 1 , 0)));
			Commentary("refreshCalculation CloseLong =" + Text(IIF ( CloseLong, 1 , 0)));
			Commentary("refreshCalculation CloseShort =" + Text(IIF ( CloseShort, 1 , 0)));
			
			return false;	
	}
	
Events

	//Bar更新事件函数，参数indexs表示变化的数据源图层ID数组
	//这里不做任何信号，之根据信号和仓位对比，做出调整。
	OnBar(ArrayRef<Integer> indexs)
	{
		Commentary("OnBar");

		secCount = secCount +1;

		if (SyncWithPastSignal and  Mod(secCount,5) ==0){
			secCount = 0;
			
			if(BarStatus == 2)
			{
				Commentary("OnBar1");

				AvgTR = AvgTrueRange(10);
				Commentary("OnBarClose AvgTR[1] =" + Text(AvgTR[1]));
				//openUnits = getUnitsNumber(Close, AvgTR[1]);	

				//获取当前商品的仓位
				Position pos;
				A_DeleteAccountOrder(Symbol);
				Bool ret = A_GetPosition(symbol, pos, "", 0);

				if (ret == True){
					Commentary("OnBar2");
					
					Bool closetrading = CloseTradingTime();
					Bool opentrading = OpenTradingTime();


					if (MarketPosition ==0){// or closetrading == True
						Commentary("OnBar3");
						Commentary("OnBar3 pos.shortCanCoverVolume = " + Text(pos.shortCanCoverVolume));
						Commentary("OnBar3 pos.longCanSellVolume = " + Text(pos.longCanSellVolume));
					
						closeWithBuyToCover(pos.shortCanCoverVolume ,Close);
						closeWithSell(pos.longCanSellVolume,Close);
					}                

					if (MarketPosition >0){
						Commentary("OnBar4");
						closeWithBuyToCover(pos.shortCanCoverVolume ,Close);
						
						Commentary("OnBar4 pos.longActiveVolume = " + Text(pos.longActiveVolume));
						Commentary("OnBar4 opentrading = " + text(IIF (opentrading, 1 , 0)));

						if (pos.longActiveVolume== 0  And opentrading == True){
							Commentary("OnBar42");
							
							if (pos.longCurrentVolume == 0 ){ // 只在第SyncEntries次开仓跟，SyncEntries次开仓后跟风险大
								Commentary("OnBar421 CurrentEntries = " + Text(CurrentEntries));
								
								if (CurrentEntries <= SyncEntries)
									pureBuy(abs(longCurrentContracts), Close);
							}
							Else {
								Commentary("OnBar422 CurrentEntries = " + Text(CurrentEntries));
								
								if (abs(longCurrentContracts) - pos.longCurrentVolume > 0) {
									pureBuy(abs(abs(longCurrentContracts) - pos.longCurrentVolume), Close);
								}
								else {
									closeWithSell(abs(abs(longCurrentContracts) - pos.longCurrentVolume), Close);
								}
							}
						}
					}

					if (MarketPosition <0){
						Commentary("OnBar5");

						closeWithSell(pos.longCanSellVolume, Close);
						Commentary("OnBar5 pos.shortActiveVolume = " + Text(pos.shortActiveVolume));
						Commentary("OnBar5 opentrading = " + text(IIF (opentrading, 1 , 0)));

						if (pos.shortActiveVolume == 0  And opentrading == True)
						{
							Commentary("OnBar52");
							
							if (pos.shortCurrentVolume == 0){
								Commentary("OnBar521 CurrentEntries = " + Text(CurrentEntries));
								
								if (CurrentEntries <=SyncEntries)
									pureSellShort(abs(shortCurrentContracts),Close);
							}
							Else {
								Commentary("OnBar522 CurrentEntries = " + Text(CurrentEntries));

								if (abs(shortCurrentContracts) - pos.shortCurrentVolume > 0) {
									pureSellShort(abs(abs(shortCurrentContracts) - pos.shortCurrentVolume),Close);
								} else {
									closeWithBuyToCover(abs(abs(shortCurrentContracts) - pos.shortCurrentVolume) ,Close);
								}
							}
						}
					}
					
				}
			}		
		}
	}

	//下一个Bar开始前，重新执行当前bar最后一次，参数为当前bar的图层数组
	//所有信号来自  OnBarClose(), 信号发出未成交的仓位，由OnBar()  去调整
	OnBarClose(ArrayRef<Integer> indexs)
	{
		//Print("OnBarClose");
		Commentary("OnBarClose #1  sendorder = " + text(IIF (sendorder, 1 , 0)));
	
		
        if (sendorder == false) {
			Commentary("OnBarClose #2 本周期没有下单。");
			A_DeleteAccountOrder(Symbol);
			refreshCalculation();

			if( closetrading == True ){ // 收尾平仓
				Commentary("OnBarClose #3 收尾平仓。");
				Commentary("OnBarClose #3 收尾平仓 longCurrentContracts = " + Text(longCurrentContracts));
				Commentary("OnBarClose #3 收尾平仓 shortCurrentContracts = " + Text(shortCurrentContracts));
		
				If(longCurrentContracts > 0 ) // 有多仓的情况
				{
					
					Commentary("OnBarClose #4 ##多仓平 closetrading() == True "+ Text(Close) );
					Sell(longCurrentContracts, Close, Enum_Signal_NotSend);//Enum_Signal_NotSend
					currentPosition =0;
					sendorder= true;
				}
					
				If(shortCurrentContracts > 0) // 有空仓的情况
				{
					Commentary("OnBarClose #5 ##空仓平 closetrading() == True "+ Text(Close) );
					BuyToCover(shortCurrentContracts, Close, Enum_Signal_NotSend);//
					currentPosition =0;
					sendorder= true;
				}
				
				Return;
			} else {
				Commentary("OnBarClose #6 交易时间。");
				
				If(MarketPosition>0)  // 有多仓的情况, 满足平仓条件
				{
					Commentary("OnBarClose #7 MarketPosition>0");
					Commentary("OnBarClose #7 MarketPosition>0 LastEntryPrice= " + Text(LastEntryPrice) + "AvgTR= " + Text(AvgTR) + "Close= " + Text(Close) );

					//平仓
					if (CloseLong or failclose or fallbackclose ) { //or  Close < ma5 or //ma10 > ma5
						Commentary("OnBarClose #71 ##多仓平仓条件满足，平仓价 ="+ Text(Close));
						Commentary("OnBarClose #71 ##多仓平仓条件满足, closetrading() == True "+ Text(Close) + "longCurrentContracts = " + Text(longCurrentContracts));
						Commentary("OnBarClose #71 ##多仓平仓条件满足，failclose ="+ text(IIF (failclose, 1 , 0)));
						Commentary("OnBarClose #71 ##多仓平仓条件满足，fallbackclose ="+ text(IIF (fallbackclose, 1 , 0)));
						
						
						Sell(longCurrentContracts(), Close, Enum_Signal_NotSend);   //close  all order  Enum_Signal_NotSend
						currentPosition =0;
						sendorder= true;
					}
					else {
						//加仓
						Commentary("OnBarClose #72 ##加仓检查");
						Commentary("OnBarClose #72 ##加仓检查 CurrentEntries = " + Text(CurrentEntries));
						Commentary("OnBarClose #72 ##加仓检查 limitedEntries = " + Text(limitedEntries));
						Commentary("OnBarClose #72 ##加仓检查 LastEntryPrice + AvgTR * incNate < Close = " + Text( IIF ( LastEntryPrice + AvgTR * incNate < Close, 1 , 0)));

						if ( longEntries < limitedEntries)
						{
							Commentary("OnBarClose #721 ##加仓检查 ");
							
							if (LastEntryPrice + AvgTR * incNate < Close)
							{
								Commentary("OnBarClose #7211 ##加仓检查");
								
								Buy(openUnits, Close, Enum_Signal_NotSend); //Enum_Signal_NotSend
								currentPosition = openUnits;
								sendorder= true;								
							}
						}
					}
					
				}
				

				If(MarketPosition <0) // 有空仓的情况, 满足平仓条件
				{
					
					Commentary("OnBarClose #8 MarketPosition<0");
					Commentary("OnBarClose #8 MarketPosition>0 LastEntryPrice= " + Text(LastEntryPrice) + "AvgTR= " + Text(AvgTR) + "Close= " + Text(Close) );
					
					//平仓
					if (CloseShort or  failclose  or fallbackclose ) { // Close > ma5 or  //ma10 < ma5 
						Commentary("OnBarClose #81 ##空仓平仓条件满足，平仓价 ="+ Text(Close));
						Commentary("OnBarClose #71 ##多仓平仓条件满足, closetrading() == True "+ Text(Close) + "shortCurrentContracts = " + Text(shortCurrentContracts));
						Commentary("OnBarClose #81 ##空仓平仓条件满足，failclose ="+ text(IIF (failclose, 1 , 0)));
						Commentary("OnBarClose #81 ##空仓平仓条件满足，fallbackclose ="+ text(IIF (fallbackclose, 1 , 0)));						
						
						
						BuyToCover(shortCurrentContracts, Close, Enum_Signal_NotSend); //close  all order, Enum_Signal_NotSend
						currentPosition =0;
						//sendorder= true;
					}
					else {
						//加仓
						Commentary("OnBarClose #82 ##加仓检查");
						Commentary("OnBarClose #82 ##加仓检查 CurrentEntries = " + Text(CurrentEntries));
						Commentary("OnBarClose #82 ##加仓检查 limitedEntries = " + Text(limitedEntries));
						Commentary("OnBarClose #82 ##加仓检查 LastEntryPrice - AvgTR * incNate > Close = " + Text(IIF(LastEntryPrice - AvgTR * incNate > Close, 1 , 0)));							
						
						if ( shortEntries < limitedEntries)
						{
							Commentary("OnBarClose #821 ##加仓检查");
							if (LastEntryPrice - AvgTR * incNate > Close)
							{
								Commentary("OnBarClose #8211 ##加仓检查");
								SellShort(openUnits, Close, Enum_Signal_NotSend); //Enum_Signal_NotSend
								currentPosition = openUnits;
								//sendorder= true;								
							}
						}
					}
				}

				Commentary("OnBar9 开仓前检查 opentrading = " + text(IIF (opentrading, 1 , 0)));
				Commentary("OnBar9 开仓前检查 closetrading = " + text(IIF (closetrading, 1 , 0)));

				If( MarketPosition == 0 and opentrading == True){ //sendorder == false and
					Commentary("OnBarClose #9 ##开仓检查 MarketPosition = 0");
					
					if (OpenLong)
					{
						Commentary("OnBarClose #911 @@@@@ 开仓价====多" + Text(Close));
						if(openUnits >=1)
						{
							Commentary("OnBarClose #9111 @@@@@ 开仓价====openUnits=" + Text(openUnits));
							Buy(openUnits, Close, Enum_Signal_NotSend); //Enum_Signal_NotSend
							currentPosition = openUnits;
							sendorder= true;
						}
					}

					if (OpenShort) 
					{
						Commentary("OnBarClose #921 @@@@@ 开仓价====空" + Text(Close));
						if(openUnits >=1)
						{
							Commentary("OnBarClose #9211 @@@@@ 开仓价====openUnits=" + Text(openUnits));
						
							SellShort(openUnits, Close, Enum_Signal_NotSend); //Enum_Signal_NotSend
							currentPosition=-openUnits;
							sendorder= true;
						}
					}
				}
			}
		} else{
			Commentary("本周期已经下单过了。");
		}
		
        Commentary("MRatio =" + Text(MRatio));

	}

    OnBarOpen(ArrayRef<Integer> indexs)
    {
   		//Print("OnBarOpen");
	    sendorder =false;
//		updateMarginRatio();
	}
	
	//处理买入卖出信号
    OnSignal(ArrayRef<Signal> sigs)
    {
    }

	
	//此处实现事件函数
	OnInit(){
	}
	
	OnReady(){
		sendorder =false;
		LastMarketPosition =0;
		limitedEntries = nEntries;
		MRatio =0;
		updateMarginRatio();
    }

	//策略账户仓更新事件函数，参数pos表示更新的账户仓结构体
	OnStrategyPosition(PositionRef pos)
	{
	}

	//委托更新事件函数，参数ord表示更新的委托结构体
	OnOrder(OrderRef ord)
	{
	}
	
	OnFill(FillRef ordFill)
	{
	}
	
	//持仓更新事件函数，参数pos表示更新的持仓结构体
	OnPosition(PositionRef pos)
	{
	}


	//当前策略退出时触发
	OnExit()
	{

	}

//------------------------------------------------------------------------
// 编译版本	2022/05/23 152528
// 版权所有	savy2021
// 更改声明	TradeBlazer Software保留对TradeBlazer平台
//			每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------
